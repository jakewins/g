
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

expr = { string | id ~ prop_lookup? }

id = ${ ( ASCII_ALPHA | "_" | "-" ) + }

prop_lookup = { ("." ~ id)+ }

string = ${ "\"" ~ str_inner ~ "\"" }
str_inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

map = {
  "{" ~ "}" |
  "{" ~ map_pair ~ ("," ~ map_pair)* ~ "}"
}
map_pair = { id ~ ":" ~ expr }


node = { "(" ~ id? ~ (":" ~ id)? ~ map? ~ ")" }

rel = { "<"? ~ "-" ~ ( "[" ~ id? ~ (":" ~ id)? ~ map? ~ "]" )? ~ "-" ~ ">"? }

pattern = { node  ~ ( rel ~ node )* }

create_clause = { "CREATE" ~ pattern }

match_clause = { "MATCH" ~ pattern }
return_clause = { "RETURN" ~ projection }

projection = { expr ~ ("AS" ~ id)? ~ ( "," ~ expr ~ ("AS" ~ id)? )* }

create_stmt = { create_clause }
match_stmt = { match_clause ~ return_clause? }

statement = { create_stmt | match_stmt }
query = { SOI ~ ( statement )* ~ EOI }