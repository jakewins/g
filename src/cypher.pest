
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

expr = { string | prop_lookup | id }

id = ${ ( ASCII_ALPHA | "_" | "-" ) + }

prop_lookup = { id ~ ("." ~ id)+ }

string = ${ "\"" ~ str_inner ~ "\"" }
str_inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

map = {
  "{" ~ "}" |
  "{" ~ map_pair ~ ("," ~ map_pair)* ~ "}"
}
map_pair = { id ~ ":" ~ expr }


node = { "(" ~ id? ~ ( ":" ~ label )? ~ map? ~ ")" }
label = { id }

rel = { left_arrow? ~ "-" ~ ( "[" ~ id? ~ ( ":" ~ rel_type )? ~ map? ~ "]" )? ~ "-" ~ right_arrow? }
rel_type = { id }
left_arrow = { "<" }
right_arrow = { ">" }

pattern = { node  ~ ( rel ~ node )* }

projection = { expr ~ ("AS" ~ id)? }
reduce_projections = { ( aggregation | projection ) ~ ( "," ~ ( aggregation | projection ) )* }
map_projections = { ( !aggregation ~ projection ) ~ ( "," ~ ( !aggregation ~ projection ) )* }
projections = { map_projections | reduce_projections }

count_agg = { "COUNT(" ~ expr? ~ ")" }
sum_agg = { "SUM(" ~ expr? ~ ")" }
agg = _{ count_agg | sum_agg }
aggregation = { agg ~ ("AS" ~ id)?  }

create_stmt = { "CREATE" ~ pattern }
match_stmt = { "MATCH" ~ pattern }
return_stmt = { "RETURN" ~ projections }

statement = _{ create_stmt | match_stmt }
query = { SOI ~ ( statement )* ~ return_stmt? ~ EOI }