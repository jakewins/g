
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

expr = { and_expr ~ (^"OR " ~ and_expr)* }
and_expr = { term ~ (^"AND " ~ term)* }
term = _{ binary_op | atom }

binary_op = { atom ~ op ~ atom }
op = ${ "=" | ">" }

atom = _{ bool | float | int | prop_lookup | count_call | func_call | string | id | list | "(" ~ expr ~ ")" }

id = ${ ( ASCII_ALPHA | "_" | "-" ) ~ ( ASCII_ALPHANUMERIC | "_" | "-" )* }

prop_lookup = { id ~ ("." ~ id)+ }

func_call = { id ~ "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }
count_call = { ^"COUNT" ~ "(" ~ "*" ~ ")" }

string = ${
  "\"" ~ dblq_str_inner ~ "\"" |
  "'" ~ singleq_str_inner ~ "'" }
dblq_str_inner = @{ dbldq_char* }
dbldq_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}
singleq_str_inner = @{ singleq_char* }
singleq_char = {
    !("'" | "\\") ~ ANY
    | "\\" ~ ("'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

bool = _{ lit_true | lit_false }
lit_true = { ^"TRUE" }
lit_false = { ^"FALSE" }

int = @{
    "-"? ~ ( "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* )
}

float = @{
   "-"? ~ ( "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ) ~ "." ~ ASCII_DIGIT*
}

map = {
  "{" ~ "}" |
  "{" ~ map_pair ~ ("," ~ map_pair)* ~ "}"
}
map_pair = { id ~ ":" ~ expr }

list = {
  "[" ~ "]" |
  "[" ~ expr ~ ("," ~ expr)* ~ "]"
}

// Multiple labels are supported for CREATE but not yet for MATCH
node = { "(" ~ id? ~ ( ":" ~ label )* ~ map? ~ ")" }
label = { id }

rel = { left_arrow? ~ "-" ~ ( "[" ~ id? ~ ( ":" ~ rel_type )? ~ map? ~ "]" )? ~ "-" ~ right_arrow? }
rel_type = { id }
left_arrow = { "<" }
right_arrow = { ">" }

patterns = _{ pattern ~ ( "," ~ pattern )* }
pattern = { node ~ ( rel ~ node )* }

projection = { expr ~ (^"AS" ~ id)? }
projections = { projection ~ ( "," ~ projection )* }
project_all = { "*" }

distinct_clause = { ^"DISTINCT" }

where_clause = { ^"WHERE" ~ expr }

order_clause = { ^"ORDER BY" ~ order_expr ~ ( "," ~ order_expr )* }
order_expr = { expr ~ (^"DESC")? }

skip_clause = { ^"SKIP" ~ expr }
limit_clause = { ^"LIMIT" ~ expr }

create_stmt = { ^"CREATE" ~ patterns }
match_stmt = { ^"MATCH" ~ pattern ~ where_clause? }
with_stmt = { ^"WITH" ~ distinct_clause? ~ projections ~ where_clause? ~ order_clause? ~ skip_clause? ~ limit_clause? }
unwind_stmt = { ^"UNWIND" ~ expr ~ ^"AS" ~ id }
return_stmt = { ^"RETURN" ~ distinct_clause? ~ ( projections | project_all ) ~ order_clause? ~ skip_clause? ~ limit_clause? }

statement = _{ create_stmt | match_stmt | unwind_stmt | with_stmt }
query = { SOI ~ ( statement )* ~ return_stmt? ~ EOI }