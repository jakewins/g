
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

expr = { prop_lookup | func_call | string | id }

id = ${ ( ASCII_ALPHA | "_" | "-" ) + }

prop_lookup = { id ~ ("." ~ id)+ }

func_call = { id ~ "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }

string = ${ "\"" ~ str_inner ~ "\"" }
str_inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

map = {
  "{" ~ "}" |
  "{" ~ map_pair ~ ("," ~ map_pair)* ~ "}"
}
map_pair = { id ~ ":" ~ expr }

// Multiple labels are supported for CREATE but not yet for MATCH
node = { "(" ~ id? ~ ( ":" ~ label )* ~ map? ~ ")" }
label = { id }

rel = { left_arrow? ~ "-" ~ ( "[" ~ id? ~ ( ":" ~ rel_type )? ~ map? ~ "]" )? ~ "-" ~ right_arrow? }
rel_type = { id }
left_arrow = { "<" }
right_arrow = { ">" }

pattern = { node  ~ ( rel ~ node )* }

projection = { expr ~ ("AS" ~ id)? }
projections = { projection ~ ( "," ~ projection )* }

create_stmt = { "CREATE" ~ pattern }
match_stmt = { "MATCH" ~ pattern }
return_stmt = { "RETURN" ~ projections }

statement = _{ create_stmt | match_stmt }
query = { SOI ~ ( statement )* ~ return_stmt? ~ EOI }